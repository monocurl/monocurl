[monocurl version: "0.1.0" type: "scene"]

[slide name: "config"]
	background = BLACK
	camera = Camera:
		near: 0.1 
		far: 10
		up: {0,1,0}
		origin: {0,0,4}
		target: {0,0,0}
[slide name: "slide_1"]
	let samples = 64
	
	func col(u, v) = identity:
		/* Try thinking about why these functions are useful */
		/* especially in the context of the mobius strip */
		/* how does (u - 0.5) ** 2 look at u = 0 and u = 1? */
		/* notice how the second term is "rotationally symmetric */
		
		element: {4 * (u - 0.5) ** 2, u + (1 - 2 * u) * v, 1, 1}
		
	tree uv = ColorGrid:
		x_min: 0
		x_max: 1
		y_min: 0
		y_max: 1
		x_step: 1 / samples
		y_step: 1 / samples
		tag: {}
		color_at(pos): col(pos[0], pos[1])
		stroke: CLEAR
	p += Set:
		vars&: uv
	
	let scale = 0.5
	uv = PointMapped:
		mesh: uv
		point_map(point): identity:
			let theta = point[0] * TAU 
			let t = (point[1] - 0.5) * 2
			let x = cos(theta) * (2 + t * cos(theta / 2))
			let y = sin(theta) * (2 + t * cos(theta / 2))	
			let z = t * sin(theta / 2)	
			element: vec_mul({x, y, z}, scale)
	p += Transform:
		meshes&: uv
		time: 4
